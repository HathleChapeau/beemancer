plugins {
	id 'java-library'
	id 'maven-publish'
	id 'net.neoforged.moddev' version '2.0.107'
	id("org.jetbrains.gradle.plugin.idea-ext") version "1.1.8"
}

apply(from: "./gradle/java.gradle")
apply(from: "./gradle/property_loader.gradle")

boolean dev = System.getenv("RELEASE") == null || System.getenv("RELEASE") == "false"
String buildNumber = System.getenv("BUILD_NUMBER")
//String gitHash = calculateGitHash() + (hasUnstaged() ? "-modified" : "")


base {
	archivesName = "$mod_id-$minecraft_version"
	group = mod_group
	version = mod_version + (dev && buildNumber != null ? "-${buildNumber}" : "")
}

println("Java: ${System.getProperty("java.version")}, JVM: ${System.getProperty("java.vm.version")} (${System.getProperty("java.vendor")}), Arch: ${System.getProperty("os.arch")}")

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
	version = neoforge_version

	runs {
		client {
			client()
		}
		server {
			server()
		}
	}

	mods {
		beemancer {
			sourceSet sourceSets.main
		}
	}
}


repositories {
	mavenCentral()
	maven { url = "https://maven.createmod.net" } // Ponder, Flywheel
	maven { url = "https://maven.ithundxr.dev/snapshots" } // Registrate
	maven { url = "https://maven.blamejared.com" } // JEI, Vazkii's Mods
	maven { url = "https://harleyoconnor.com/maven" } // Dynamic Trees
	maven { url = "https://maven.theillusivec4.top/" } // Curios API
	maven { url = "https://maven.squiddev.cc" } // CC: Tweaked
	maven { url = "https://www.cursemaven.com" }
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://maven.ftb.dev/releases" } // FTB Mods
	maven { url = "https://maven.architectury.dev" } // Arch API
	maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" }
	// NeoForge config api port, needed by ponder
	maven {
		url = "https://jm.gserv.me/repository/maven-public" // JourneyMap
		content {
			includeGroup "info.journeymap"
			includeGroup "mysticdrew"
		}
	}
}

dependencies {
	jarJar(implementation("com.tterrag.registrate:Registrate:$registrate_version"))

	compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-$minecraft_version:$flywheel_version")
	runtimeOnly(jarJar("dev.engine-room.flywheel:flywheel-neoforge-$minecraft_version:$flywheel_version") {
		version {
			strictly "[1.0,2.0)"
			prefer flywheel_version
		}
	})
	runtimeOnly("dev.engine-room.vanillin:vanillin-neoforge-$minecraft_version:$vanillin_version")

	jarJar(implementation("net.createmod.ponder:ponder-neoforge:$ponder_version+mc$minecraft_version"))

	compileOnly("mezz.jei:jei-$minecraft_version-common-api:$jei_version")
	compileOnly("mezz.jei:jei-$minecraft_version-neoforge-api:$jei_version")
	implementation("mezz.jei:jei-$minecraft_version-neoforge:$jei_version")

	compileOnly("top.theillusivec4.curios:curios-neoforge:$curios_version+$minecraft_version:api")
	runtimeOnly("top.theillusivec4.curios:curios-neoforge:$curios_version+$minecraft_version")

	compileOnly("maven.modrinth:sodium:mc$minecraft_version-$sodium_version-neoforge")

	if (cc_tweaked_enable.toBoolean()) {
		compileOnly("cc.tweaked:cc-tweaked-$minecraft_version-core-api:$cc_tweaked_version")
		compileOnly("cc.tweaked:cc-tweaked-$minecraft_version-forge-api:$cc_tweaked_version")
		runtimeOnly("cc.tweaked:cc-tweaked-$minecraft_version-forge:$cc_tweaked_version")
	}

	runtimeOnly("dev.architectury:architectury-neoforge:13d.0.8")
	implementation("dev.ftb.mods:ftb-chunks-neoforge:2101.1.1")
	implementation("dev.ftb.mods:ftb-teams-neoforge:2101.1.0")
	implementation("dev.ftb.mods:ftb-library-neoforge:2101.1.3")

	//implementation("maven.modrinth:journeymap:1.21.1-6.0.0-beta.46+neoforge")
	//implementation("info.journeymap:journeymap-api-neoforge:2.0.0-1.21.1-SNAPSHOT") {
	//	exclude(group: "curse.maven") // Pulls in a old version of journey map which is for forge
	//}

	//compileOnly("curse.maven:xaeros-world-map-317780:6778114")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources).configureEach {
	var replaceProperties = [
			minecraft_version      : minecraft_version,
			minecraft_version_range: minecraft_version_range,
			neo_version            : neoforge_version,
			neo_version_range      : neoforge_version_range, // Make sure to add this to gradle.properties!
			loader_version_range   : loader_version_range,
			mod_id                 : mod_id,
			mod_name               : mod_name,
			mod_version            : mod_version,
			mod_authors            : mod_author, // Maps 'mod_author' from props to '${mod_authors}' in TOML
			mod_description        : mod_description
	]
	inputs.properties replaceProperties

	filesMatching(['META-INF/neoforge.mods.toml']) {
		expand replaceProperties
	}
}